import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id('java')
    id("org.springframework.boot") version "3.5.7" apply false
    id("io.spring.dependency-management") version "1.1.5" apply false
    id('jacoco')
}

allprojects {
    group = "me.namila.service.auth"
    version = "0.0.1"

    repositories {
        mavenCentral()
        maven { url = uri("https://repo.spring.io/milestone") }
    }
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'
    apply plugin: 'jacoco'
  
    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }
    
    test {
        systemProperty "net.bytebuddy.experimental", "true"
        finalizedBy jacocoTestReport
    }
    
    jacoco {
        toolVersion = "0.8.13"
    }
    
    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = false
            csv.required = false
            html.required = true
            html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
        }
    }

    dependencies {
        // Lombok for reducing boilerplate
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)

        // Testing dependencies
        testImplementation(libs.junit.jupiter)
        testImplementation(libs.mockito.core)
        testImplementation(libs.mockito.junit.jupiter)
        testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }
}

// Aggregated Jacoco Report Task
tasks.register('jacocoAggregatedReport', JacocoReport) {
    dependsOn subprojects*.test
    
    // Collect all source directories from subprojects
    subprojects.each { subproject ->
        if (subproject.pluginManager.hasPlugin('java')) {
            sourceDirectories.from(subproject.sourceSets.main.allSource.srcDirs)
            classDirectories.from(subproject.sourceSets.main.output)
            executionData(subproject.tasks.withType(Test))
        }
    }
    
    reports {
        xml.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/aggregate/jacocoTestReport.xml")
        csv.required = false
        html.required = true
        html.outputLocation = file("${buildDir}/reports/jacoco/aggregate")
    }
}

// Bind aggregated report to build
build.dependsOn jacocoAggregatedReport