import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id('java')
    id("org.springframework.boot") version "3.5.7" apply false
    id("io.spring.dependency-management") version "1.1.5" apply false
}

allprojects {
    group = "me.namila.service.auth"
    version = "0.0.1"

    repositories {
        mavenCentral()
        maven { url = uri("https://repo.spring.io/milestone") }
    }
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }

    }
    test {
        systemProperty "net.bytebuddy.experimental", "true"
    } // To enable experimental features in ByteBuddy used by Mockito for Java25

    dependencies {
        // Lombok for reducing boilerplate
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)

        // Testing dependencies
        testImplementation(libs.junit.jupiter)
        testImplementation(libs.mockito.core)
        testImplementation(libs.mockito.junit.jupiter)
        testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
//        options.compilerArgs.addAll(listOf("-parameters", "-Xlint:unchecked"))
    }

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }
}

